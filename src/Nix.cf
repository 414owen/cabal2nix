-- Run "bnfc -p Hackage4Nix -d Nix.cf" to re-generate the parsers from
-- this file.
--
-- The grammar works fine for 99% of all expressions. The remaining
-- percent doesn't parse because of 'let' or 'with' statements, which
-- don't support (and probably don't want to, because we can't resolve
-- them).

Expr . Expr ::= "{" [Argument] "}" ":" "cabal.mkDerivation" "(" Argument ":" Dictionary ")";

Argument . Argument ::= Ident ;
separator Argument "," ;

Dictionary . Dictionary ::= "{" [Attribute] "}" ;

Attribute . Attribute ::= Reference "=" Value ;
terminator Attribute ";" ;

Reference . Reference ::= [Ident] ;
separator nonempty Ident "." ;

StringV     . Value ::= String ;
QStringV    . Value ::= QString ;
URLV        . Value ::= URL ;
PathV       . Value ::= Path ;
BoolV       . Value ::= Boolean ;
AttributeV  . Value ::= Reference ;
ListV       . Value ::= "[" [Value] "]" ;
DictionaryV . Value ::= Dictionary ;
FunctionV   . Value ::= Reference Dictionary ;
separator Value "" ;

Yes . Boolean ::= "true" ;
No  . Boolean ::= "false" ;

comment "#" ;
comment "/*" "*/" ;

token QString {"''"} char* {"''"} ;
token URL letter+ {"://"} (char - [" \n\t;[]{}"])+ ;
token Path {"./"} (char - [" \n\t;[]{}"])+ ;
